| inputaoc 2024-10.txt
| eventstats c as h
| streamstats c as y
| eval data = printf("%0".len(h)."d:_%s_", y, _raw)
| eventstats values(data) as data
| eval
    i = mvrange(0, len(_raw) + 2),
    data = mvappend(
        mvjoin(mvmap(i, "_"), ""),
        mvmap(data, replace(data, "^\d+:", "")),
        mvjoin(mvmap(i, "_"), "")
    ),
    x = mvrange(0, len(mvindex(data, 0))),
    x = mvmap(x, if(mvindex(split(_raw, ""), x)  == "0", x + 1, null()))
| fields data x y
| where isnotnull(x)
| mvexpand x
| eval
    vx = mvappend("-1", "+0", "+1", "+0"),
    vy = mvappend("+0", "-1", "+0", "+1"),
    routes = x.",".y
| foreach 1 2 3 4 5 6 7 8 9 [
    | eval
        i = "<<FIELD>>",
        j = mvrange(0, mvcount(routes)),
        k = mvrange(0, 4)
    | foreach mode=multivalue j [
        | eval
            current_route = mvindex(routes, '<<ITEM>>'),
            pos = mvindex(split(current_route, ";"), -1),
            x = mvindex(split(pos, ","), 0),
            y = mvindex(split(pos, ","), 1),
            neighbours = mvappend(
                substr(mvindex(data, y + 0), x + 0, 1),
                substr(mvindex(data, y - 1), x + 1, 1),
                substr(mvindex(data, y + 0), x + 2, 1),
                substr(mvindex(data, y + 1), x + 1, 1)
            ),
            next = mvmap(
                k,
                if(tonumber(mvindex(neighbours, k)) == i, k, null())
            ),
            next = mvmap(
                next,
                sum(x, tonumber(mvindex(vx, next))).",".sum(y, tonumber(mvindex(vy, next)))
            ),
            routes = mvappend(
                routes,
                mvmap(next, current_route.";".next)
            )
    ]
    | eval
        routes = mvmap(
            routes,
            if(mvcount(split(routes, ";")) < i + 1, null(), routes)
        )
]
| fields data vx vy current_route pos x y neighbours next routes reachable_peaks score
| eval
    reachable_peaks = mvdedup(mvmap(
        routes,
        mvindex(split(routes, ";"), -1)
    )),
    score = mvcount(reachable_peaks)
| stats sum(score)
